plugins {
    id 'java-library'
    id 'org.openjfx.javafxplugin' version "$jfxPlugin"
    id 'biz.aQute.bnd.builder' version "$bndPlugin"
    id 'com.vanniktech.maven.publish' version "$mavenPublishPlugin"
    id 'org.javamodularity.moduleplugin' version "$modularityPlugin"
}

group 'io.github.palexdev'
version "$virtualizedfxVersion"

repositories {
    mavenCentral()

    flatDir {
        dirs 'libs'
    }
}

sourceCompatibility = "$jdk"
targetCompatibility = "$jdk"

javafx {
    version = "$jfx"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
    configuration = 'compileOnly'
}

configurations {
    testImplementation.extendsFrom compileOnly
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit"

    testImplementation "org.testfx:testfx-core:$testfx"
    testImplementation "org.testfx:testfx-junit5:$testfx"
    testImplementation "fr.brouillard.oss:cssfx:$cssfx"
    testImplementation "net.datafaker:datafaker:$faker"
    testImplementation name: 'scenicview'
    testImplementation name: "materialfx"

    api "io.github.palexdev:mfxcore:$mfxcore"
    api "io.github.palexdev:mfxeffects:$mfxeffects"
    api "io.github.palexdev:mfxresources:$mfxresources"
}

javadoc {
    excludes = ['**/*.html', 'META-INF/**']

    options.use = true
    options.splitIndex = true
    options.encoding = 'UTF-8'
    options.author = true
    options.version = true
    options.windowTitle = "$project.name $project.version API"
    options.docTitle = "$project.name $project.version API"
    options.links = ['https://docs.oracle.com/en/java/javase/11/docs/api',
                     'https://openjfx.io/javadoc/18']
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourcesJarBuild(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJarBuild
    archives jar
}

jar {
    manifest {
        attributes(
                'Bundle-Name': project.name,
                'Bundle-Description': "Alternative VirtualFlows for JavaFX",
                'Bundle-SymbolicName': 'io.github.palexdev',
                'Export-Package': 'io.github.palexdev.virtualizedfx.*'
        )
    }
}

allprojects {
    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublish {
            sonatypeHost = "S01"
        }
    }
}

task removeBnd(type: Delete) {
    delete fileTree(project.buildDir) {
        include '**/*.bnd'
    }
}

build.dependsOn removeBnd

compileTestJava {
    moduleOptions {
        compileOnClasspath = true
    }
    sourceCompatibility = "$testJdk"
    targetCompatibility = "$testJdk"
}

test {
    useJUnitPlatform()

    moduleOptions {
        runOnClasspath = true
    }
}